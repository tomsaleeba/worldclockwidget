buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.3'
    }
}

apply plugin: 'com.android.application'

apply from: rootProject.file('gradle/codequality.gradle')

android {
    compileSdkVersion 30

    defaultConfig {
        versionCode 101
        versionName "1.9.1"

        minSdkVersion 14
        targetSdkVersion 30


        buildConfigField 'String', 'BUILD_TAG', '"' + getBuildTag() + '"'
        buildConfigField 'String', 'OWM_API_KEY', '"' + getOpenWeatherMapApiKey() + '"'
        buildConfigField 'boolean', 'ENABLE_WEATHER', 'true'

        def buildSuffix = getBuildSuffix(versionName, versionCode)
        applicationVariants.all { variant ->
            def file = variant.outputs[0].outputFile
       // variant.outputs[0].outputFile = new File(file.parent, file.name.replace(".apk", "-" + buildSuffix + ".apk"))
        }
    }

    if (project.hasProperty('signingKeyStoreFile')) {
        signingConfigs {
            release {
                storeFile     file(signingKeyStoreFile)
                storePassword signingKeyStorePassword
                keyAlias      signingKeyAlias
                keyPassword   signingKeyPassword
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true

            proguardFile 'proguard-project.txt'
            if (project.hasProperty('signingKeyStoreFile')) {
                signingConfig signingConfigs.release
            }
        }
    }

    if (rootProject.hasProperty("lintAbortOnError") && lintAbortOnError == 'false') {
        lintOptions {
            abortOnError false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

def getBuildSuffix(versionName, versionCode) {
    def suffix = versionName + '-' + versionCode
    if (System.getenv()['BUILD_NUMBER'] != null) {
        suffix += '-b' + System.getenv()['BUILD_NUMBER']
    }
    return suffix
}

def getBuildTag() {
    def tag = ''
    if (System.getenv()['BUILD_NUMBER'] != null) {
        tag += 'b' + System.getenv()['BUILD_NUMBER']
    } else {
        tag += 'l'
    }
    tag += '@' + new Date().format('yyyyMMdd')
    return tag
}

def getOpenWeatherMapApiKey() {
    if (project.hasProperty('owmApiKey')) {
        return owmApiKey
    } else {
        def apiKeyFile = file('default_owm_api_key')
        if (apiKeyFile.isFile()) {
          return apiKeyFile.text.trim()
        }
    }
    return 'NOKEY'
}

///////////////////////////////////////////////////
// Dependencies

repositories {
    mavenCentral()
}

dependencies{
    //implementation 'com.actionbarsherlock:actionbarsherlock:4.4.0@aar'
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    //testCompile 'junit:junit:4.12'
    //implementation 'com.android.support:support-v4:28.0.0'
    //implementation 'com.android.support:cardview-v7:28.0.0'
    //implementation 'com.android.support:appcompat-v7:28.0.0'
    //implementation 'com.android.support:design:28.0.0'
    //implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.code.gson:gson:2.8.7'
    implementation group: 'net.time4j', name: 'time4j-android', version: '4.2-2018g'
}


///////////////////////////////////////////////////
// Checkstyle

task checkstyleDebug(type: Checkstyle, dependsOn: 'compileDebugSources') {
    source = fileTree('src/main/java/')
    classpath = files('build/intermediates/classes/debug')
}
check.dependsOn checkstyleDebug

///////////////////////////////////////////////////
// Findbugs

task findbugsDebug(type: FindBugs, dependsOn: 'compileDebugSources') {
    source = fileTree('src/main/java/')
    classes = fileTree('build/intermediates/classes/debug')
    classpath = files() // empty classpath!
    effort = 'max'
    excludeFilter = rootProject.file('config/findbugs/androidExcludeFilter.xml')
}
check.dependsOn findbugsDebug

///////////////////////////////////////////////////
// PMD

task pmd(type: Pmd) {
    source = fileTree('src/main/java/')
    ruleSets = ['java-basic', 'java-braces', 'java-android']
}
check.dependsOn 'pmd'
